plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.agadasom'
version = '0.0.1'

// java 17와의 호환성을 보장하기 위해 사용
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17) // Enforces Java 17
    }
}


repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'org.apache.poi:poi:5.4.1'
    implementation 'org.apache.poi:poi-scratchpad:5.4.1'
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.25.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.25.1'
    runtimeOnly 'ch.qos.logback:logback-classic:1.5.19'
}

test {
    useJUnitPlatform()
}

tasks.withType(Jar).configureEach {
    from(rootDir) {
        include 'license.txt'
        into 'META-INF' // JAR 파일 내 META-INF 디렉터리에 포함
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'hwpdoc' // 프로젝트 이름
                description = 'Hancom 에서 개발한 HWP 포맷 파일에 접근할 수 있는 방법을 제공하는 라이브러리' // 프로젝트 설명
                url = 'github.com/juanPionero/hwpdoc' // 프로젝트 URL (소스 코드 위치 등)

                // 라이선스 정보 등록
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0' // 라이선스 이름
                        url = 'www.apache.org' // 라이선스 URL
                    }
                }

                // 개발자(제작자) 정보 등록
                developers {
                    developer {
                        id = 'juanPionero' // 개발자 ID (사용자 이름)
                        name = 'Seung Hoon Lee' // 개발자 실명
                        email = 'juanlee0@naver.com' // 개발자 이메일
                    }
                }

                // SCM(소스 관리) 정보 등록
                scm {
                    connection = 'scm:git:git://github.com/juanPionero/hwpdoc.git'
                    developerConnection = 'scm:git:ssh://github.com/juanPionero/hwpdoc.git'
                    url = 'github.com/juanPionero/hwpdoc'
                }
            }
        }
    }
}
